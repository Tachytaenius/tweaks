ACTOR _LostSoul: LostSoul replaces LostSoul
{
	Health 25
	+NOBLOOD
}

ACTOR _Cacodemon: Cacodemon replaces Cacodemon
{
	bloodcolor "blue"
	Health 500
}

ACTOR _PainElemental: PainElemental replaces PainElemental
{
	States{
		Missile:
			PAIN D 5 A_FaceTarget
			PAIN E 5 A_FaceTarget
			PAIN F 5 BRIGHT A_FaceTarget
			PAIN F 0 BRIGHT A_PainAttack("_LostSoul", 10, PAF_AIMFACING, 16)
			Goto See
	}
}

ACTOR MarinePainsaw: MarineChainsaw replaces MarineChainsaw
{
	States
	{
		Melee:
			PLAY E 2 A_M_Saw("weapons/sawfull", "weapons/sawhit", 4, "ChainsawPainPuff")
			PLAY E 0 A_M_SawRefire
			Goto Melee
			PLAY A 0
			Goto See
		Missile:
			Stop
	}
}

ACTOR _Revenant: Revenant replaces Revenant
{
	+NOBLOOD
	States{
		Missile:
		SKEL J 10 A_FaceTarget 
		SKEL K 10 BRIGHT A_SkelMissile
		SKEL J 10 A_FaceTarget
		SKEL K 10 BRIGHT A_SkelMissile
		Goto See
	}
}

ACTOR _Archvile replaces Archvile
{
	Health 500
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 128
	Monster
	MaxTargetRange 512
	+QUICKTORETALIATE
	+FLOORCLIP
	+NOTARGET
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/stop"
	Obituary "$OB_VILE"
	States
	{
		Spawn:
			VILE AB 10 A_Look
			Loop
		See:
			VILE AABBCCDDEEFF 2 A_Chase
			Loop
		Missile:
			VILE G 0 BRIGHT A_VileStart
			VILE G 10 BRIGHT A_FaceTarget
			VILE H 8 BRIGHT A_VileTarget
			VILE IJKLMN 8 BRIGHT A_FaceTarget 
			VILE O 8 BRIGHT A_VileAttack("vile/stop", 10, 35)
			VILE P 20 BRIGHT
			Goto See
		Pain:
			VILE Q 5   
			VILE Q 5 A_Pain
			Goto See
		Death:
			VILE Q 7
			VILE R 7 A_Scream
			VILE S 7 A_NoBlocking
			VILE TUVWXY 7
			VILE Z -1
			Stop
	}
}

//ACTOR _Demon: Demon replaces Demon // It looks REALLY bad.
//{
//	States{
//		XDeath:
//			SARG O 8
//			SARG P 8 A_XScream
//			SARG Q 8 A_NoBlocking
////			SARG R 8 // It looks bad.
//			SARG S 8
//			SARG T -1
//			Stop
//	}
//}

ACTOR Cacobus: _Cacodemon 20676
{
	bloodcolor "orange"
	Health 600
	Speed 10
	DropItem "LightningGun"
	Obituary "%o was zapped by a cacobus."
	HitObituary "%o was swallow whole by a cacobus."
	States{
		Missile:
			CBUS B 5 A_FaceTarget
			CBUS C 5 A_FaceTarget
			TNT1 A 0 A_jump(64, "Small1")
			TNT1 A 0 A_jump(64, "Small2")
			TNT1 A 0 A_jump(64, "Large1")
			CBUS D 5 BRIGHT A_CustomComboAttack("LightningBall2", 32, 11 * random(1, 6))
			Goto See
		Large1:
			CBUS D 5 BRIGHT A_CustomComboAttack("LightningBall1", 32, 11 * random(1, 6))
			Goto See
		Small1:
			CBUS D 5 BRIGHT A_CustomComboAttack("SmallLightningBall1", 32, 10 * random(1, 6))
			Goto See
		Small2:
			CBUS D 5 BRIGHT A_CustomComboAttack("SmallLightningBall2", 32, 10 * random(1, 6))
			Goto See
		Spawn:
			CBUS A 10 A_Look
			Loop
		See:
			CBUS A 3 A_Chase
			Loop
		Pain:
			CBUS E 3
			CBUS E 3 A_Pain
			CBUS F 6
			Goto See
		Death:
			CBUS G 8
			CBUS H 8 A_Scream
			CBUS I 8
			CBUS J 8
			CBUS K 8 A_NoBlocking
			CBUS L -1 A_SetFloorClip
			Stop
		Raise:
			CBUS L 8 A_UnSetFloorClip
			CBUS KJIHG 8
			Goto See
	}
}

ACTOR _ZombieMan: ZombieMan replaces ZombieMan
{
	Health 45
	decal "BulletChip"
	DropItem "Pistol"
	States{
		Missile:
			POSS E random(7, 8) A_FaceTarget
			POSS E 0 A_PlaySound ("grunt/attack")
			POSS E 0 A_CustomMissile("BulletCasing",4,-45+random(0,8))
			POSS F 8 A_CustomBulletAttack (22.5, 10, 1, 6)
			POSS E 8
			goto See
	}
}

ACTOR _ShotgunGuy: ShotgunGuy replaces ShotgunGuy
{
	Health 45
	decal "BulletChip"
	States{
		Missile:
			SPOS E random(7, 8) A_FaceTarget
			SPOS F 0 bright A_PlaySound("shotguy/attack", CHAN_WEAPON)
			SPOS F 0 A_CustomMissile("ShotgunShell",4,-45+random(0,8))
			SPOS F 10 Bright A_CustomBulletAttack(6.6, 6.6, 5, 1, "BulletPuff", 0, CBAF_AIMFACING)
			SPOS E 10
			Goto See
	}
}

ACTOR RifleGuy 20664
{
	Health 45
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "grunt/attack"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was peppered by a rifle guy."
	DropItem "Rifle"
	decal BulletChip
	States
	{
	Spawn:
		RPOS AB 10 A_Look
		Loop
	See:
		RPOS AABBCCDD 4 A_Chase
		Loop
	Missile:
		RPOS E random(7, 8) A_FaceTarget
		RPOS E 0 A_PlaySound ("grunt/attack")
		RPOS E 0 A_CustomMissile("BulletCasing",4,-45+random(0,8))
		RPOS F 7 A_CustomBulletAttack(0.25, 0.25, 1, 3, "BulletPuff", 0, CBAF_AIMFACING)
		RPOS E 3
		RPOS E 0 A_CPosRefire
		Goto Missile+1
	Pain:
		RPOS G 3
		RPOS G 3 A_Pain
		Goto See
	Death:
		RPOS H 5
		RPOS I 5 A_Scream
		RPOS J 5 A_NoBlocking
		RPOS K 5
		RPOS L -1
		Stop
	XDeath:
		RPOS M 5
		RPOS N 5 A_XScream
		RPOS O 5 A_NoBlocking
		RPOS PQRST 5
		RPOS U -1
		Stop
	Raise:
		RPOS K 5
		RPOS JIH 5
		Goto See
	}
}

ACTOR AutoShotgunGuy 20671
{
	Health 45
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "weapons/sshotf"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was overwhelmed by an auto-shotgun guy."
	DropItem "AutoShotgun"
	decal BulletChip
	States
	{
	Spawn:
		APOS AB 10 A_Look
		Loop
	See:
		APOS AABBCCDD 4 A_Chase
		Loop
	Missile:
		APOS E random(7, 8) A_FaceTarget
		APOS E 0 A_PlaySound ("grunt/attack")
		APOS E 0 A_CustomMissile("ShotgunShell",4,-45+random(0,8))
		APOS F 8 BRIGHT A_CustomBulletAttack(45, 45, 15, 1, "BulletPuff", 0, CBAF_AIMFACING)
		APOS E 8
		APOS E 0 A_CPosRefire
		Goto Missile+1
	Pain:
		APOS G 3
		APOS G 3 A_Pain
		Goto See
	Death:
		APOS H 5
		APOS I 5 A_Scream
		APOS J 5 A_NoBlocking
		APOS K 5
		APOS L -1
		Stop
	XDeath:
		APOS M 5
		APOS N 5 A_XScream
		APOS O 5 A_NoBlocking
		APOS PQRST 5
		APOS U -1
		Stop
	Raise:
		APOS K 5
		APOS JIH 5
		Goto See
	}
}

ACTOR RevolverGuy 20668
{
	Health 45
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	decal BulletChip
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "weapons/revllf"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was splashed against the wall by a revolver guy."
	DropItem "Revolver"
	States
	{
	Spawn:
		REVP AB 10 A_Look
		Loop
	See:
		REVP AABBCCDD 4 A_Chase
		Loop
	Missile:
		REVP E random(7, 8) A_FaceTarget
		REVP E 0 A_CustomMissile("BulletCasing",4,-45+random(0,8))
		REVP F 10 BRIGHT A_CustomBulletAttack(12, 12, 1, 3, "BulletPuff", 0, CBAF_AIMFACING)
		REVP E 19
		Goto See
	Pain:
		REVP G 3
		REVP G 3 A_Pain
		Goto See
	Death:
		REVP H 5
		REVP I 5 A_Scream
		REVP J 5 A_NoBlocking
		REVP K 5
		REVP L -1
		Stop
	XDeath:
		REVP M 5
		REVP N 5 A_XScream
		REVP O 5 A_NoBlocking
		REVP PQRST 5
		REVP U -1
		Stop
	Raise:
		REVP K 5
		REVP JIH 5
		Goto See
	}
}

ACTOR RailgunGuy 20665
{
	Health 45
	Radius 20
	Height 56
	Mass 100
	Speed 8
	decal RailgunScorch
	PainChance 170
	Monster
	+FLOORCLIP
	SeeSound "shotguy/sight"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "%o was picked off by a railgun guy."
	DropItem "Railgun"
	Damage 20
	States
	{
		Spawn:
			RGPS AB 10 A_Look
			Loop
		See:
			RGPS AABBCCDD 3 A_Chase
			Loop
		Missile:
			RGPS E 15 A_FaceTarget
			RGPS E 0 A_CustomMissile("ShotgunShell",4,-45+random(0,8))
			RGPS F 4 BRIGHT A_CustomRailgun(20, 0, "gray", "blue", RGF_FULLBRIGHT, 0, 4, "BulletPuff", 0.1, 0.1, 8192, 12, 2, 2, "none", 0, 180, 32)
			RGPS E 42
			Goto See
		Pain:
			RGPS G 3
			RGPS G 3 A_Pain
			Goto See
		Death:
			RGPS H 5
			RGPS I 5 A_Scream
			RGPS J 5 A_NoBlocking
			RGPS K 5
			RGPS L -1
			Stop
		XDeath:
			RGPS M 5
			RGPS N 5 A_XScream
			RGPS O 5 A_NoBlocking
			RGPS PQRST 5
			RGPS U -1
			Stop
		Raise:
			RGPS L 5
			RGPS KJIH 5
			Goto See
	}
}

ACTOR MinigunGuy: ChaingunGuy replaces ChaingunGuy
{
	Health 100
	decal "BulletChip"
	Obituary "%o was perforated by a minigunner."
	AttackSound "weapons/chngun"
	States{
		Missile:
			CPOS E random(7, 8) A_FaceTarget
			CPOS F 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			CPOS F 0 A_CustomMissile("BulletCasing",4,-45+random(0,8))
			CPOS F 2 bright A_CustomBulletAttack(11, 11, 1, 1, "BulletPuff", 0, CBAF_AIMFACING)
			CPOS E 2
			CPOS F 0 A_CPosRefire
			goto Missile+1
	}
}

ACTOR MicroMissilesGuy 20667
{
	Health 100
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	decal RailgunScorchLower
	+FLOORCLIP
	SeeSound "chainguy/sight"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	ActiveSound "chainguy/active"
	AttackSound "weapons/rocklf"
	Obituary "%o was blasted by a micro missiles guy."
	Dropitem "MicroMissileLauncher"
	States
	{
		Spawn:
			MPOS AB 10 A_Look
			Loop
		See:
			MPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			MPOS E random(7, 8) A_FaceTarget
			MPOS F 0 Bright A_CustomMissile("MicroMissileWeak", 48, 0, random(-3, 3), 0, random(-3, 3))
			MPOS F 0 Bright A_CustomMissile("MicroMissileWeak", 48, 0, random(-3, 3), 0, random(-3, 3))
			MPOS F 0 Bright A_CustomMissile("MicroMissileWeak", 48, 0, random(-3, 3), 0, random(-3, 3))
			MPOS F 0 Bright A_CustomMissile("MicroMissileWeak", 48, 0, random(-3, 3), 0, random(-3, 3))
			MPOS F 0 Bright A_CustomMissile("MicroMissileWeak", 48, 0, random(-3, 3), 0, random(-3, 3))
			MPOS F 0 Bright A_CustomMissile("MicroMissileWeak", 48, 0, random(-3, 3), 0, random(-3, 3))
			MPOS F 18 A_PlaySound("weapons/rocklf", CHAN_AUTO)
			Goto See
		Pain:
			MPOS G 3
			MPOS G 3 A_Pain
			Goto See
		Death:
			MPOS H 5
			MPOS I 5 A_Scream
			MPOS J 5 A_NoBlocking
			MPOS KLM 5
			MPOS N -1
			Stop
		XDeath:
			MPOS O 5
			MPOS P 5 A_XScream
			MPOS Q 5 A_NoBlocking
			MPOS RS 5
			MPOS T -1
			Stop
		Raise:
			MPOS N 5
			MPOS MLKJIH 5
			Goto See
	}
}

ACTOR _WolfensteinSS: WolfensteinSS replaces WolfensteinSS
{
	Health 60
	AttackSound "weapons/revllf"
	DropItem "MachineGun"
	States{
		Missile:
			SSWV E random(7, 8) A_FaceTarget
			SSWV F 3 A_FaceTarget
			SSWV G 0 A_CustomMissile("BulletCasing",4,45-random(0,8))
			SSWV G 3 BRIGHT A_CustomBulletAttack(8, 8, 1, 2, "BulletPuff", 0, CBAF_AIMFACING)
			SSWV F 0 A_CPosRefire
			Goto Missile+1
	}
}